plugins {
  // Apply the java and library plugin to add support for Java
  id 'application'
}

application {
  mainClassName = 'com.yworks.example.HelloWorld'
}

compileJava {
  sourceCompatibility '1.7'
  targetCompatibility '1.7'
}

jar {
  manifest {
    attributes(
      'Main-Class': application.mainClassName
    )
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.yworks:yguard:4.0.0'
}

task obfuscate   {
  dependsOn jar
  group 'yGuard'
  description 'Obfuscates and shrinks the java archive.'

  doLast {
    ant.taskdef(
            name: 'yguard',
            classname: 'com.yworks.yguard.YGuardTask',
            classpath: sourceSets.main.runtimeClasspath.asPath
    )

    def archivePath = jar.archiveFile.get().asFile.path
    ant.yguard {
      inoutpair(in: archivePath, out: archivePath.replace(".jar", "_obf.jar"))
      // don't let the obfuscator remove the "Deprecated" attributes from the .class file entries
      attribute(name: "Deprecated")
      shrink(logfile: "${buildDir}/yshrink.log.xml") {
        keep {
          'class'(classes: 'protected', methods: 'protected', fields: 'protected')
        }
      }
      rename(mainclass: application.mainClassName, logfile: "${buildDir}/yguard.log.xml") {
        keep {
          'class'(classes: 'protected', methods: 'protected', fields: 'protected')
        }
      }
    }
  }
}
