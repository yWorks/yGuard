plugins {
  // Apply the java and library plugin to add support for Java
  id 'application'
}

application {
  mainClassName = 'com.yworks.example.HelloWorld'
}

jar {
  manifest {
    attributes(
      'Main-Class': application.mainClassName
    )
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.yworks:yguard:2.9.+'
}

task obfuscate {
  dependsOn jar
  group 'yGuard'
  description 'Obfuscates and shrinks the java archive.'

  doLast {
    ant.taskdef(
            name: 'yguard',
            classname: 'com.yworks.yguard.YGuardTask',
            classpath: sourceSets.main.runtimeClasspath.asPath
    )

    def archivePath = jar.archiveFile.get().asFile.path
    ant.yguard {
      inoutpair(in: archivePath, out: archivePath.replace(".jar", "_obf.jar"))
      shrink(logfile: "${buildDir}/yshrink.log.xml") {
        keep {
          method(name: "void main(java.lang.String[])", "class": application.mainClassName)
          // serializable classes
          'class'(implements: "java.io.Serializable", classes: "private", methods: "private", fields: "private")
          // menu items loaded per reflection
          'class'(extends: "com.yworks.example.MyMenuItem", classes: "friendly", methods: "public", fields: "public")
        }
      }
      rename(mainclass: application.mainClassName, logfile: "${buildDir}/yguard.log.xml") {
        keep {
          method(name: "void readObject(java.io.ObjectInputStream)")
          method(name: "void writeObject(java.io.ObjectInputStream)")
          field(name: "serialVersionUID")
          'class'(extends: "com.yworks.example.MyMenuItem", classes: "friendly", methods: "public", fields: "public")
        }
        property(name: "error-checking", value: "pedantic")
      }
    }
  }
}
